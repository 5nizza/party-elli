INFO {
  TITLE:       "LTL -> DBA  -  Formula Theta From LtlNfBa Paper"
  DESCRIPTION: "Conversion of LTL to Deterministic Buchi Automaton"
  SEMANTICS:   Moore
  TARGET:      Mealy
}

GLOBAL {
  PARAMETERS {
    n = 1;
  }        

  DEFINITIONS {
    // always eventually p_i for 0<=i<=n-1 
    all_event(bus) =
      &&[0 <= i < SIZEOF bus]
        (G F bus[i]);      
  } 
}
           
MAIN {         

  INPUTS {
    p[n]; // input propositions
    q; // "request" signal
    r; // "grant" signal
  }

  OUTPUTS {
    acc; // accepting state
  }

  GUARANTEES {
    !((all_event(p)) -> G(q-> F r))
      <-> G F acc;
  }

}

