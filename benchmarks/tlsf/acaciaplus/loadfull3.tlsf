INFO {
  TITLE:       "Load Balancing  -  3 Clients"
  DESCRIPTION: "One of the Acacia+ Example files"
  SEMANTICS:   Moore
  TARGET:      Mealy
}

MAIN {

  INPUTS {
    idle;
    request0;
    request1;
    request2;
  }

  OUTPUTS {
    grant0;
    grant1;
    grant2;
  }

  ASSUMPTIONS {
    G F idle;
    G (!(idle && X !grant0 && X !grant1 && X !grant2) || X idle);
    G (X !grant0 || X ((!request0 && !idle) U (!request0 && idle)));
  }

  INVARIANTS {
    X !grant0 || request0;
    X !grant1 || request1;
    X !grant2 || request2;
    X !grant0 || X !grant1;
    X !grant0 || X !grant2;
    X !grant1 || X !grant2;
    !request0 || X !grant1;    
    !request0 || X !grant2;    
    (X !grant0 && X !grant1 && X !grant2) || idle;    
  }

  GUARANTEES {
    ! F G (request0 && X !grant0);
    ! F G (request1 && X !grant1);
    ! F G (request2 && X !grant2);
  }

}
